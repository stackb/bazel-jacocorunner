load("@rules_java//java:defs.bzl", "java_binary", "java_library")

java_binary(
    name = "coverageoutputgenerator",
    visibility = ["//visibility:public"],
    runtime_deps = [":coverageoutputgenerator_lib"],
)

java_library(
    name = "coverageoutputgenerator_lib",
    srcs = glob(["*.java"]),
    deps = [
        "//third_party:auto_value",
        "@jacoco_maven//:com_beust_jcommander",
        "@jacoco_maven//:com_google_code_findbugs_jsr305",
        "@jacoco_maven//:com_google_code_gson_gson",
        "@jacoco_maven//:com_google_guava_guava",
    ],
)

java_library(
    name = "BranchCoverage",
    srcs = ["BranchCoverage.java"],
    deps = [
        "//third_party:auto_value",
        "@jacoco_maven//:com_google_guava_guava",
    ],
)

genrule(
    name = "coverage_lcov_merge_tools",
    srcs = [":all_lcov_merger_tools_deploy.jar"],
    outs = ["coverage/all_lcov_merger_tools_deploy.jar"],
    cmd = "cp $< $@",
)

genrule(
    name = "zip",
    srcs = [":coverageoutputgenerator_deploy.jar"],
    outs = ["coverageoutputgenerator.zip"],
    cmd = """
cp $(location coverageoutputgenerator_deploy.jar) ./coverageoutputgenerator.jar
$(location @bazel_tools//tools/zip:zipper) c $@ ./coverageoutputgenerator.jar
""",
    tools = ["@bazel_tools//tools/zip:zipper"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "release_notes_md",
    srcs = ["coverageoutputgenerator.zip"],
    outs = ["release_notes.md"],
    cmd = """
$(location @bazel_tools//tools/build_defs/hash:sha256) $(location coverageoutputgenerator.zip) sha256.txt
sha256=$$(cat sha256.txt)
cat << EOF > $@

> TODO: edit this release, download and attach the coverageoutputgenerator.zip file
> to the release notes on github, update the download url, and remove this TODO placeholder.

To use this release asset:

~~~bazel
http_archive(
    name = "bazel_coverageoutputgenerator",
    sha256 = "$$sha256",
    urls = ["https://github.com/stackb/bazel-coverageoutputgenerator/files/XXXXXXXX/coverageoutputgenerator.XXXX.zip"],
    build_file_content = 'filegroup(name = "jar", srcs = ["coverageoutputgenerator.jar"], visibility = ["//visibility:public"])',
)
~~~
EOF
""",
    tools = ["@bazel_tools//tools/build_defs/hash:sha256"],
    visibility = ["//visibility:public"],
)
